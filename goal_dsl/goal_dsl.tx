/*
    Goal DSL
*/

GoalDSLModel:
    imports*=Import
    (
        (target=Target)?
        (goals*=Goal)
        (tConstraints*=TimeConstraint)
        (middleware*=CommunicationMiddleware)
    )#  // Any order
;


Goal: TopicGoal | AreaGoal | MotionGoal | TrajectoryGoal | ComplexGoal |
GoalSequence;

AreaGoal: RectangleAreaGoal | CircularAreaGoal | StraightLineAreaGoal |
MovingAreaGoal | PolylineAreaGoal;

MotionGoal: OrientationGoal | PositionGoal | PoseGoal;

TopicGoal: TopicMessageReceivedGoal | TopicMessageParamGoal;

TrajectoryGoal: StraightLineTrajectoryGoal | WaypointTrajectoryGoal;

CommunicationMiddleware: AMQPBroker | MQTTBroker | RedisBroker;

BrokerAuth: BrokerAuthPlain;

BrokerAuthPlain:
    'AuthPlain' '(' username=STRING ',' password=STRING ')'
;

AMQPBroker:
    'AMQPBroker' name=ID '->' '{'
    (
        ('host:' host=STRING ';')
        ('port:' port=INT ';')
        ('vhost:' vhost=STRING ';')
        ('exchange:' exchange=STRING ';')?
        ('username:' username=STRING ';')?
        ('password:' password=STRING ';')?
    )#
    '}'
;

MQTTBroker:
    'MQTTBroker' name=ID '->' '{'
    (
        ('host:' host=STRING ';')
        ('port:' port=INT ';')
        ('username:' username=STRING ';')?
        ('password:' password=STRING ';')?
    )#
    '}'
;

RedisBroker:
    'RedisBroker' name=ID '->' '{'
    (
        ('host:' host=STRING ';')
        ('port:' port=INT ';')
        ('db:' db=INT ';')?
        ('auth:' auth=BrokerAuth ';')
    )#
    '}'
;

Target:
    'Target' name=ID '->' '{'
    (
        ('goals:' '[' goals*=[Goal|FQN][','] ']' ';')
        ('middleware:' middleware=[CommunicationMiddleware] ';')
    )#
    '}'
;

GoalSequence:
    'Sequence' name=ID '->' '{'
    (
        ('addGoal(' goals+=[Goal|FQN] ')' ';')*
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
    )#
    '}'
;

ComplexGoal:
    'ComplexGoal' name=ID '->' '{'
    (
        ('algorithm:' algorithm=[ComplexGoalAlgorithmType] ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('xAccomplished:' xAccomplished=INT ';')?
        ('addGoal(' goals+=[Goal|FQN] ')' ';')*
    )#
    '}'
;

ExpressionGroup:
    '(' r1=PrimitiveExpressionRule ')' operator=BooleanOperationType '('
    r2=PrimitiveExpressionRule ')'
;

PrimitiveExpressionRule: StringExpressionRule | NumericExpressionRule;

StringExpressionRule:
    param=STRING operator=StringOperationType val=STRING
;

NumericExpressionRule:
    param=STRING operator=NumericOperationType val=NUMBER
;

StringOperationType: '~' | '!~' | '==' | '!=';

NumericOperationType: '>' | '<' | '==' | '!=';

BooleanOperationType: 'AND' | 'OR' | 'NOT' | 'XOR' | 'NOR' | 'XNOR' | 'NAND';

// TOPIC GOALS ----------------------------------------------------->

TopicMessageReceivedGoal:
    'TopicMsgReceivedGoal' name=ID '->' '{'
    (
        ('topic:' topic=STRING ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
    )#
    '}'
;

TopicMessageParamGoal:
    'TopicMsgParamGoal' name=ID '->' '{'
    (
        ('topic:' topic=STRING ';')
        ('expression:' expression=PrimitiveExpressionRule ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
    )#
    '}'
;

// <-----------------------------------------------------------------

// AREA GOALS ------------------------------------------------------>

RectangleAreaGoal:
    'RectangleAreaGoal' name=ID '->' '{'
    (
        ('center:' centerPoint=Point ';')
        ('lengthX:' lengthX=NUMBER ';')
        ('lengthY:' lengthY=NUMBER ';')
        ('type:' type=AreaGoalType ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#  // Any order
    '}'
;

PolylineAreaGoal:
    'PolylineAreaGoal' name=ID '->' '{'
    (
        ('points:' '[' points+=Point[','] ']' ';')
        ('type:' type=AreaGoalType ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#  // Any order
    '}'
;

MovingAreaGoal:
    'MovingAreaGoal' name=ID '->' '{'
    (
        ('radius:' radius=NUMBER ';')
        ('type:' type=AreaGoalType ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#  // Any order
    '}'
;


CircularAreaGoal:
    'CircularAreaGoal' name=ID '->' '{'
    (
        ('center:' centerPoint=Point ';')
        ('radius:' radius=NUMBER ';')
        ('type:' type=AreaGoalType ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

StraightLineAreaGoal:
    'StraightLineAreaGoal' name=ID '->' '{'
    (
        ('startPoint:' startPoint=Point ';')
        ('endPoint:' endPoint=Point ';')
        ('type:' type=AreaGoalType ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

// <-----------------------------------------------------------------

// TRAJECTORY GOALS ------------------------------------------------>

StraightLineTrajectoryGoal:
    'StraightLineTrajGoal' name=ID '->' '{'
    (
        ('startPoint:' startPoint=Point ';')
        ('finishPoint:' finishPoint=Point ';')
        ('maxDeviation:' maxDeviation=NUMBER ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

WaypointTrajectoryGoal:
    'WaypointTrajectoryGoal' name=ID '->' '{'
    (
        ('points:' '[' points+=Point[','] ']' ';')
        ('maxDeviation:' maxDeviation=NUMBER ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;
// <----------------------------------------------------------------

PoseGoal:
    'PoseGoal' name=ID '->' '{'
    (
        ('orientation:' orientation=Orientation ';')
        ('position:' position=Point ';')
        ('maxDeviationPos:' maxDeviationPos=NUMBER ';')
        ('maxDeviationOri:' maxDeviationOri=NUMBER ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

OrientationGoal:
    'OrientationGoal' name=ID '->' '{'
    (
        ('orientation:' orientation=Orientation ';')
        ('maxDeviation:' maxDeviation=NUMBER ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

PositionGoal:
    'PositionGoal' name=ID '->' '{'
    (
        ('position:' point=Point ';')
        ('maxDeviation:' maxDeviation=NUMBER ';')
        ('timeConstraint:' timeConstraint=[TimeConstraint|FQN] ';')?
        ('listening:' listeningTopic=STRING ';')?
    )#
    '}'
;

Point: Point2D | Point3D;
Orientation: Orientation2D | Orientation3D;

Point2D:
    'Point2D' '(' xAxis=NUMBER ',' yAxis=NUMBER ')'
;

Point3D:
    'Point3D' '(' xAxis=NUMBER ',' yAxis=NUMBER ',' zAxis=NUMBER ')'
;

Orientation2D:
    'Orientation2D' '(' zAxis=NUMBER ')'
;

Orientation3D:
    'Orientation3D' '(' xAxis=NUMBER ',' yAxis=NUMBER ',' zAxis=NUMBER ')'
;

TimeConstraint: TimeConstraintDuration;

TimeConstraintDuration:
    'TCDuration' name=ID '->' '{'
    (
        ('type:' type=TimingConstraintType ';')
        ('time:' comparator=TCExpressionComparator time=NUMBER ';')
    )#
    '}'
;


// Follow this: https://github.com/textX/textX/blob/master/tests/functional/test_scoping/test_reference_to_buildin_attribute.py
/* Assignment: goalRef=[Goal|FQN].goalParam */

TCExpressionComparator: '>' | '<' | '==';

AreaGoalType: 'ENTER' | 'EXIT' | 'AVOID' | 'STEP';

ComplexGoalAlgorithmType: 'ALL_ACCOMPLISHED' | 'NONE_ACCOMPLISHED' |
'AT_LEAST_ONE' | 'EXACTLY_X_ACCOMPLISHED' | 'ALL_ACCOMPLISHED_ORDERED' |
'EXACTLY_X_ACCOMPLISHED_ORDERED';

ParamValueConstraintType: 'EXACT_VALUE' | 'WITHIN_RANGE' | 'OUT_OF_RANGE' |
'EXISTS_IN_COLLECTION';

TimingConstraintType: 'FROM_APP_START' | 'FROM_GOAL_START';

FQN: ID('.'ID)*;
Import: 'import' importURI=FQN ('as' name=ID)? ';';

// Special rule for comments. Comments start with //

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;
